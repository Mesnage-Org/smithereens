(*
  The PG Structural Language
  ==========================
*)

Muropeptide = Monomer , { Connector , Monomer } , [ Connector ] , [ { " " }- ,
  ( Modifications , [ { " " }- , Crosslinks ]
  | Crosslinks , [ { " " }- , Modifications ]
  ) ] ;

Connector
  = "=" (* Crosslink *)
  | "~" (* Glycosidic Bond *)
  | ( "~=" | "=~" ) (* Both *)
  ;

Monomer
  = Glycan
  | Peptide
  | Glycan , "-" , Peptide
  ;

Glycan = { Monosaccharide , [ Modifications ] }- ;

Peptide = { Amino Acid , [ Modifications ] ,
  [ Lateral Chain ] }- ;

Lateral Chain = "[" , [ "<" (* C-to-N *) | ">" (* N-to-C *) ] ,
  { Amino Acid , [ Modifications ] }- , "]" ;

Monosaccharide = lowercase ;

Amino Acid = uppercase ;

Modifications = "(" ,
  ( Predefined Modification
  | Chemical Offset
  ) , { { " " } , "," , { " " } ,
  ( Predefined Modification
  | Chemical Offset
  ) } , ")" ;

Predefined Modification = [ Multiplier ] , letter ,
  { letter | digit | "_" } ;

Chemical Offset = { ( "+" | "-" ) , [ Multiplier ] ,
  { Element , [ Integer ] }- }- ;

Crosslinks = "(" , Crosslink Descriptors ,
  { { " " } , "," , { " " } , Crosslink Descriptors } , ")" ;

Crosslink Descriptors = Crosslink Descriptor ,
  { { " " } , "&" , { " " } , Crosslink Descriptor } ;

Crosslink Descriptor = position ,
  ( "-" (* Donor-Acceptor *)
  | "=" (* Acceptor=Donor *)
  ) , position ;

Multiplier = Integer , "x" ;

Element = uppercase , [ lowercase ] ;

Integer = { digit }- ;

letter = uppercase | lowercase ;

uppercase
  = "A" | "B" | "C" | "D" | "E" | "F" | "G"
  | "H" | "I" | "J" | "K" | "L" | "M" | "N"
  | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
  | "V" | "W" | "X" | "Y" | "Z"
  ;

lowercase
  = "a" | "b" | "c" | "d" | "e" | "f" | "g"
  | "h" | "i" | "j" | "k" | "l" | "m" | "n"
  | "o" | "p" | "q" | "r" | "s" | "t" | "u"
  | "v" | "w" | "x" | "y" | "z"
  ;

digit
  = "0" | "1" | "2" | "3" | "4" | "5" | "6"
  | "7" | "8" | "9"
  ;

position = "1" | "2" | "3" | "4" | "5" ;