Muropeptide = Monomer , { Connection , Monomer } , [ Connection ] , [ { " " }- ,
  ( Modifications , [ { " " }- , Crosslinks ]
  | Crosslinks , [ { " " }- , Modifications ]
  ) ] ;

Monomer
  = Glycan , "-" , Peptide
  | Peptide
  | Glycan
  ;

Connection
  = "=" (* Crosslink *)
  | "~" (* Glycosidic Bond *)
  | ( "~=" | "=~" ) (* Both *)
  ;

Modifications = "(" , ( Named Modification | Offset Modification ) ,
  { { " " } , "," , { " " } , ( Named Modification | Offset Modification ) } , ")" ;

Crosslinks = "(" , Crosslink Descriptors ,
  { { " " } , "," , { " " } , Crosslink Descriptors } , ")" ;

Glycan = { Monosaccharide , [ Modifications ] }- ;

Peptide = { Amino Acid , [ Modifications ] ,
  [ Lateral Chain ] }- ;

Named Modification = [ Multiplier ] , letter ,
  { letter | digit | "_" } ;

Offset Modification = ( "+" | "-" ) , [ Multiplier ] ,
  Chemical Composition ;

Crosslink Descriptors = Crosslink Descriptor ,
  { { " " } , "&" , { " " } , Crosslink Descriptor } ;

Monosaccharide = lowercase ;

Amino Acid = [ lowercase ] , uppercase ;

Lateral Chain = "[" , [ "<" (* C-to-N *) | ">" (* N-to-C *) ] ,
  { Amino Acid , [ Modifications ] }- , "]" ;

Multiplier = Count , "x" ;

Chemical Composition
  = { Atomic Offset }- , [ ( "+" | "-" ) , Particle Offset ]
  | Particle Offset
  ;

Crosslink Descriptor = position ,
  ( "-" (* Donor-Acceptor *)
  | "=" (* Acceptor=Donor *)
  ) , position ;

Count = digit - "0" , { digit } ;

Atomic Offset = ( Element | Isotope ) , [ Count ] ;

Particle Offset = [ Count ] , Particle ;

Element = uppercase , [ lowercase ] ;

Isotope = "[" , Count , Element , "]" ;

Particle = lowercase ;

letter = uppercase | lowercase ;

uppercase
  = "A" | "B" | "C" | "D" | "E" | "F" | "G"
  | "H" | "I" | "J" | "K" | "L" | "M" | "N"
  | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
  | "V" | "W" | "X" | "Y" | "Z"
  ;

lowercase
  = "a" | "b" | "c" | "d" | "e" | "f" | "g"
  | "h" | "i" | "j" | "k" | "l" | "m" | "n"
  | "o" | "p" | "q" | "r" | "s" | "t" | "u"
  | "v" | "w" | "x" | "y" | "z"
  ;

digit
  = "0" | "1" | "2" | "3" | "4" | "5" | "6"
  | "7" | "8" | "9"
  ;

position = "1" | "2" | "3" | "4" | "5" ;